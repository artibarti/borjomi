###########################################################
###### Borjomi status report function. ####################
###########################################################

function(borjomi_status text)
  set(status_cond)
  set(status_then)
  set(status_else)

  set(status_current_name "cond")
  foreach(arg ${ARGN})
    if(arg STREQUAL "THEN")
      set(status_current_name "then")
    elseif(arg STREQUAL "ELSE")
      set(status_current_name "else")
    else()
      list(APPEND status_${status_current_name} ${arg})
    endif()
  endforeach()

  if(DEFINED status_cond)
    set(status_placeholder_length 23)
    string(RANDOM LENGTH ${status_placeholder_length} ALPHABET " " status_placeholder)
    string(LENGTH "${text}" status_text_length)
    if(status_text_length LESS status_placeholder_length)
      string(SUBSTRING "${text}${status_placeholder}" 0 ${status_placeholder_length} status_text)
    elseif(DEFINED status_then OR DEFINED status_else)
      message(STATUS "${text}")
      set(status_text "${status_placeholder}")
    else()
      set(status_text "${text}")
    endif()

    if(DEFINED status_then OR DEFINED status_else)
      if(${status_cond})
        string(REPLACE ";" " " status_then "${status_then}")
        string(REGEX REPLACE "^[ \t]+" "" status_then "${status_then}")
        message(STATUS "${status_text} ${status_then}")
      else()
        string(REPLACE ";" " " status_else "${status_else}")
        string(REGEX REPLACE "^[ \t]+" "" status_else "${status_else}")
        message(STATUS "${status_text} ${status_else}")
      endif()
    else()
      string(REPLACE ";" " " status_cond "${status_cond}")
      string(REGEX REPLACE "^[ \t]+" "" status_cond "${status_cond}")
      message(STATUS "${status_text} ${status_cond}")
    endif()
  else()
    message(STATUS "${text}")
  endif()
endfunction()

function(mergeFlagList out_var)
  set(__result "")
  foreach(__list ${ARGN})
    foreach(__flag ${${__list}})
      string(STRIP ${__flag} __flag)
      set(__result "${__result} ${__flag}")
    endforeach()
  endforeach()
  string(STRIP ${__result} __result)
  set(${out_var} ${__result} PARENT_SCOPE)
endfunction()

function(printConfigurationSummary)

    mergeFlagList(__flags_rel CMAKE_CXX_FLAGS_RELEASE CMAKE_CXX_FLAGS)
    mergeFlagList(__flags_deb CMAKE_CXX_FLAGS_DEBUG CMAKE_CXX_FLAGS)

    borjomi_status("")
    borjomi_status("******************* borjomi configuration summary *******************")
    borjomi_status("General:")
    borjomi_status("  Version           :   ${PROJECT_VERSION}")
    borjomi_status("  System            :   ${CMAKE_SYSTEM_NAME}")
    borjomi_status("  C++ compiler      :   ${CMAKE_CXX_COMPILER}")
    borjomi_status("  Release CXX flags :   ${__flags_rel}")
    borjomi_status("  Debug CXX flags   :   ${__flags_deb}")
    borjomi_status("  Build type        :   ${CMAKE_BUILD_TYPE}")
    borjomi_status("")
    borjomi_status("  BUILD_EXAMPLES    :   ${BUILD_EXAMPLES}")
    borjomi_status("  BUILD_TESTS       :   ${BUILD_TESTS}")
    borjomi_status("  BUILD_DOCS        :   ${BUILD_DOCS}")
    borjomi_status("  BUILD_BENCHMARKS  :   ${BUILD_BENCHMARKS}")
    borjomi_status("")
    borjomi_status("Dependencies:")
    borjomi_status("  SSE               : " USE_SSE AND COMPILER_HAS_SSE_FLAG THEN "Yes" ELSE "No")
    borjomi_status("  AVX               : " USE_AVX AND COMPILER_HAS_AVX_FLAG THEN "Yes" ELSE "No")
    borjomi_status("  AVX2              : " USE_AVX2 AND COMPILER_HAS_AVX2_FLAG THEN "Yes" ELSE "No")
    borjomi_status("  Pthread           : " USE_PTHREAD THEN "Yes" ELSE "No")
    borjomi_status("  TBB               : " USE_TBB AND TBB_FOUND THEN "Yes (ver. ${TBB_INTERFACE_VERSION})" ELSE "No")
    borjomi_status("  OMP               : " USE_OMP AND OMP_FOUND THEN "Yes" ELSE "No")
    borjomi_status("  NNPACK            : " USE_NNPACK AND NNPACK_FOUND THEN "Yes" ELSE "No")
    borjomi_status("  Intel MKL         : " USE_INTEL_MKL AND INTELMKL_FOUND THEN "Yes" ELSE "No")
    borjomi_status("  CBLAS             : " USE_CBLAS AND BLAS_FOUND THEN "Yes" ELSE "No")
    borjomi_status("  OpenCL            : " USE_OPENCL AND OpenCL_FOUND THEN "Yes (ver. ${OpenCL_VERSION_STRING})" ELSE "No")
    borjomi_status("  LibDNN            : " USE_LIBDNN AND GreenteaLibDNN_FOUND THEN "Yes (ver. ${GreenteaLibDNN_VERSION})" ELSE "No")
    borjomi_status("")
    borjomi_status("Utilities:")
    borjomi_status("  OpenCV            : " USE_OPENCV AND OpenCV_FOUND THEN "Yes (ver. ${OpenCV_VERSION})" ELSE "No")
    borjomi_status("  Serializer        : " USE_SERIALIZER THEN "Yes" ELSE "No")
    borjomi_status("  Image API         : " USE_IMAGE_API THEN "Yes" ELSE "No")
    borjomi_status("  Gemmlowp          : " USE_GEMMLOWP THEN "Yes" ELSE "No")
    borjomi_status("")
    borjomi_status("Install:")
    borjomi_status("  Install path      :   ${CMAKE_INSTALL_PREFIX}")
    borjomi_status("")

endfunction()
